#!/usr/bin/env clojure
;; vim: ft=clojure

(require '[alda.core :refer :all])

(def REST-RATE 0.15)
(def MS-LOWER 30)
(def MS-UPPER 3000)
(def MAX-OCTAVE 8)

(defn random-note
  "Returns a random note in a random octave with a random duration in
  milliseconds.

  May randomly return a rest with a random duration in milliseconds, instead."
  []
  (let [ms (ms (rand-nth (range MS-LOWER MS-UPPER)))]
    (if (< (rand) REST-RATE)
      (pause (duration ms))
      (let [o (rand-int (inc MAX-OCTAVE))
            n [(keyword (str (rand-nth "abcdefg")))
               (rand-nth [:sharp :flat :natural])]]
        [(octave o)
         (note (apply pitch n) (duration ms))]))))

(play!
  (part "midi-electric-piano-1")
  (panning 25)
  (repeatedly 50 random-note)

  (part "midi-timpani")
  (panning 50)
  (repeatedly 50 random-note)

  (part "midi-celesta")
  (panning 75)
  (repeatedly 50 random-note))

(println "Press Ctrl-C to stop & exit.")

(.addShutdownHook (Runtime/getRuntime)
  (Thread. #(stop!)))
